apply plugin: "java"
apply plugin: "idea"

group   "org.alfray.trainmotion"
version "0.1-SNAPSHOT"

repositories {
    mavenCentral()
    // Journey (https://github.com/CodeBrig/Journey), a JCEF-powered web browser
    // (Note: CEF=Chrome Embedded Framework, J for java bindings from the java-cef project).
    // Offline fatJar w/ natives is multiplatform ~300 MB.
    maven { url "https://jitpack.io" }
}

compileJava {
    sourceCompatibility = "1.8"
    targetCompatibility = "1.8"
}

dependencies {
    compile "commons-cli:commons-cli:1.4"
    compile "org.bytedeco:javacv-platform:1.5.3"
    compile "org.bytedeco:javacpp-platform:1.5.3"
    compile "com.fasterxml.jackson.core:jackson-databind:2.10.2"
    // Note: jetty-server:10.0.0.beta0 requires JDK 9.
    compile "org.eclipse.jetty:jetty-server:9.4.29.v20200521"
    // Note: Journey 0.4.0 does not work. Need 0.4.1 (not avail) or older 0.3.3, either offline or online.
    // Offline version of Journey is 300 MB. Using the online (natives download at first run).
    compile "com.github.codebrig:journey:0.3.3-online"

    compile                 "com.google.dagger:dagger:2.28"
    annotationProcessor     "com.google.dagger:dagger-compiler:2.28"
    testAnnotationProcessor "com.google.dagger:dagger-compiler:2.28"

    compile             "com.google.auto.factory:auto-factory:1.0-beta7"
    annotationProcessor "com.google.auto.factory:auto-factory:1.0-beta7"

    testCompile "junit:junit:4.12"
    testCompile "com.google.truth:truth:1.0.1"
    testCompile "org.mockito:mockito-core:3.3.3"
    testCompile "com.google.guava:guava:29.0-jre"
}

// Change location of dagger generated files and map it in IJ.
def generatedDaggerDir = new File(project.projectDir.absolutePath, "src/main/dagger")
compileJava {
    doFirst {
        generatedDaggerDir.mkdirs()
    }
    // Javac javadoc: "-s dir: Specify the directory where to place generated source files"
    options.compilerArgs += [ "-s", generatedDaggerDir ]
}

clean {
    delete generatedDaggerDir
}

sourceSets {
    main {
        // Append generatedDaggerDir to the srcDir. This actually breaks javac per issue
        // JDK-8067747 as it results in sources built twice (annotation processor then javac).
        // java.srcDir generatedDaggerDir <-- don't do that.
    }
}

idea {
    module {
        // Tell IJ about our new generated source dir.
        // Tip: In Project pane > src/main/dagger > Mark Directory > Mark Generated Source Root.
        generatedSourceDirs += generatedDaggerDir
    }
}

// Rules to build a self-contained deployment fat jar with a specified entry point.
jar {
    manifest {
        attributes(
                "Main-Class": "com.alfray.trainmotion.Main",
        )
    }
}

// Build a "fat" jar with all the dependencies that are marked using "compile".
// C.f. http://stackoverflow.com/questions/4871656
// Update: this ignore these marked with the new "implementation".
task fatJar(type: Jar) {
    manifest.from jar.manifest
    classifier = "all"
    from {
        configurations.runtime.collect { it.isDirectory() ? it : zipTree(it) }
    } {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

task run(type: Exec) {
    dependsOn fatJar
    group = "Execution"
    commandLine "java", "-jar", fatJar.archiveFile.get(), "--config", "src/main/resources/config.ini"
}

artifacts {
    archives fatJar
}
